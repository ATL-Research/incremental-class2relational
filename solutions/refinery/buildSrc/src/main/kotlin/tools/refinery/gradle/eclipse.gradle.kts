/*
 * SPDX-FileCopyrightText: 2021-2023 The Refinery Authors <https://refinery.tools/>
 *
 * SPDX-License-Identifier: EPL-2.0
 */
package tools.refinery.gradle

import java.util.Properties

plugins {
	eclipse
}

// Workaround from https://github.com/gradle/gradle/issues/898#issuecomment-885765821
val eclipseResourceEncoding by tasks.registering {
	val outputFile = file(".settings/org.eclipse.core.resources.prefs")
	val encoding = providers.systemProperty("file.encoding")

	inputs.property("file.encoding", encoding)
	outputs.file(outputFile)

	doLast {
		val eclipseEncodingProperties = Properties(2)
		eclipseEncodingProperties["eclipse.preferences.version"] = "1"
		eclipseEncodingProperties["encoding/<project>"] = encoding.get()
		outputFile.outputStream().use { outputStream ->
			eclipseEncodingProperties.store(outputStream, "generated by $name")
		}
	}
}

tasks.eclipse {
	dependsOn(eclipseResourceEncoding)
}

eclipse.synchronizationTasks(eclipseResourceEncoding)

tasks.register<Delete>("clobberEclipse") {
	mustRunAfter(tasks.eclipse)
	delete(".classpath")
	delete(".project")
	delete(".settings")
}
