module Class2Relational;
-- Setup 6
create OUT : Relational from IN : class_;

-- Setup 2
uses strings;


-- if there is a configuration problem with the library string,
-- delete the two firstToLower() operations and you no longer need the library "strings"
-- the transfomarion will execute 


-- inheritance not supported yet

-- issue: choose an object-id Type (Integer, String?).
-- We choose Integer here, assuming this type is defined in the source model.

-- global variable
-- context 

-- Helper 22
helper def: objectIdType : Relational!Type =
	class_!DataType.allInstances()->select(e | e.name = 'Integer')->first();

-- Helper
helper context OclAny def: orDefault : String =
	if self.oclIsUndefined() then
		''
	else
		self
	endif;

-- Transformation
rule Class2Table {
	from
		c : class_!Class
	to
		out : Relational!Table (
			name <- c.name.orDefault,
			-- Columns are generated from Attributes in another rule not explicitly called here !
			col <- Sequence {key}->union(c.attr->select(e | not e.multiValued)),
			key <- Set {key}
		),
		key : Relational!Column (
			name <- 'objectId',
			type <- thisModule.objectIdType
		)
}

-- Transformation
rule DataType2Type {
	from
		dt : class_!DataType
	to
		out : Relational!Type (
			name <- dt.name.orDefault
		)
}

-- Transformation
rule DataTypeAttribute2Column {
	from
		a : class_!Attribute (
			a.type.oclIsKindOf(class_!DataType) and not a.multiValued and a.owner <> OclUndefined
		)
	to
		out : Relational!Column (
			name <- a.name.orDefault,
			type <- a.type
-- explicit use of implicit tracking links (first expected syntax, then present actual syntax)
--			owner <- [Class2Type.key]a.owner
--			owner <- thisModule.resolveTemp(a.owner, 'key')
		)
}

-- Transformation
rule MultiValuedDataTypeAttribute2Column {
	from
		a : class_!Attribute (
			a.type.oclIsKindOf(class_!DataType) and a.multiValued and a.owner <> OclUndefined
		)
	to
		out : Relational!Table (
			name <- a.owner.name.orDefault + '_' + a.name,
			col <- Sequence {id, value}
		),
		id : Relational!Column (
			name <- a.owner.name.orDefault.firstToLower() + 'Id',
			type <- thisModule.objectIdType
		),
		value : Relational!Column (
			name <- a.name.orDefault,
			type <- a.type
		)
}

-- Transformation
rule ClassAttribute2Column {
	from
		a : class_!Attribute (
			a.type.oclIsKindOf(class_!Class) and not a.multiValued and a.owner <> OclUndefined
		)
	to
		foreignKey : Relational!Column (
			name <- a.name.orDefault + 'Id',
			type <- thisModule.objectIdType
		)
}

-- Transformation
rule MultiValuedClassAttribute2Column {
	from
		a : class_!Attribute (
			a.type.oclIsKindOf(class_!Class) and a.multiValued and a.owner <> OclUndefined
		)
	to
		t : Relational!Table (
			name <- a.owner.name.orDefault + '_' + a.name,
			col <- Sequence {id, foreignKey}
		),
		id : Relational!Column (
			name <- a.owner.name.orDefault.firstToLower() + 'Id',
			type <- thisModule.objectIdType
		),
		foreignKey : Relational!Column (
			name <- a.name.orDefault + 'Id',
			type <- thisModule.objectIdType
		)
}

